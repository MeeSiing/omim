PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
prefix IAO: <http://purl.obolibrary.org/obo/IAO_>
prefix MONDO: <http://purl.obolibrary.org/obo/MONDO_>
prefix RO: <http://purl.obolibrary.org/obo/RO_>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix oio: <http://www.geneontology.org/formats/oboInOwl#>
prefix def: <http://purl.obolibrary.org/obo/IAO_0000115>
prefix owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?mondo_id ?hgnc_id ?omim_disease_xref ?omim_gene
WHERE
{
  ?omim_disease a owl:Class .
  ?omim_disease skos:exactMatch ?mondo_id .
  ?omim_disease rdfs:subClassOf [
        owl:onProperty RO:0004003 ;
        owl:someValuesFrom ?omim_gene
  ] .
  ?omim_gene skos:exactMatch ?hgnc_id .
  FILTER(STRSTARTS(STR(?hgnc_id), "http://identifiers.org/hgnc/"))
  FILTER(STRSTARTS(STR(?mondo_id), "http://purl.obolibrary.org/obo/MONDO_"))
  BIND(REPLACE(STR(?omim_disease), "https://omim.org/entry/", "OMIM:") AS ?omim_disease_xref)
}
